// Top-level build file where you can add configuration options common to all sub-projects/modules.

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    ext.kotlin_version = '1.3.10'
    ext.gradleVersion = '3.2.1'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.6'
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'org.jacoco:org.jacoco.core:0.8.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.palantir.jacoco-full-report" version "0.4.0"
}

apply from: 'dependencies.gradle'

apply from: rootProject.file('jacoco.gradle')

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:0.28.0"
    }

    // https://github.com/google/dagger/issues/306
    afterEvaluate {
        tasks.withType(JavaCompile.class) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }

    tasks.withType(Test) {
        testLogging {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
